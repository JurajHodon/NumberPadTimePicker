apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.philliphsu.numberpadtimepickersample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // The AndroidManifest in src/androidTest is not being merged correctly to the AndroidManifest
    // in src/main. This is a workaround to use the androidTest manifest for all build variants
    // with the debug build type. This is fine because we just don't want the additions in the
    // androidTest manifest to be compiled under the release build type.
    // https://stackoverflow.com/q/26244998/5055032
    // https://stackoverflow.com/a/25427429/5055032
    sourceSets {
        debug {
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(project(':library')) {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:preference-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:preference-v14:$rootProject.ext.supportLibraryVersion"
    compile "com.github.kizitonwose.colorpreference:support:$rootProject.ext.colorPreferenceVersion"

    testCompile "junit:junit:$rootProject.ext.junitVersion"

    // Excluding the 'support-annotations' module is necessary if you are targeting Marshmallow
    // (since Espresso hasn't moved to Marshmallow yet).
    // See:
    // https://github.com/codepath/android_guides/wiki/UI-Testing-with-Espresso
    // http://stackoverflow.com/a/36897759/5055032
    androidTestCompile("com.android.support.test:runner:$rootProject.ext.runnerVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:rules:$rootProject.ext.rulesVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
